
" Fix runtimepath. {{{

" Make sure $USERVIM is present first in runtimepath, and $USERVIM/after last.
" Also remove dupplicate and directories that don't exist.

let uvim = simplify($USERVIM)
let uvim_after = simplify($USERVIM.'/after')

let rl = split(&runtimepath, ',')
let newrl = []

for path in rl
  let path = simplify(path)
  if isdirectory(path) && -1 == index(newrl, path)
    call add(newrl, path)
  endif
endfor

if 0 != len(newrl)
  if uvim != newrl[0]
    call insert(newrl, uvim, 0)
  endif
  if uvim_after != newrl[-1]
    call add(newrl, uvim_after)
  endif
endif

let &runtimepath = join(newrl, ',')

" }}}

" Load user config (username, email, ...).
let user = $USERVIM.'/user'
if filereadable(user)
  execute 'source '.user
endif

" Not VI compatible.
set nocompatible

" Shorten some messages.
set shortmess+=aI

" No wrapping, but use ^ and $ for incomplete lines.
set nowrap
set lcs=precedes:^,extends:$

" Show line numbers.
set number
set numberwidth=5

" Try to always keep 4 lines of context around cursor.
set scrolloff=4

" To keep hidden buffers
" (so we can change buffer without
" the need to write changes...).
set hidden

" Use status line to display possible completions.
set wildmenu
set wildmode=list:longest,full

" No search highlight, incremental search.
set nohlsearch
set incsearch

" Do not redraw while running macros (much faster).
set lazyredraw

" Always show tabline.
set showtabline=2

" Increase command line history.
set history=200

" Needed so Bazaar hardlinked lightweight checkouts work ok.
set backupcopy=breakhardlink,auto

" Save info file in the user VIM directory.
let &viminfo = "!,h,'20,\"50,n".$USERVIM.'/info'

" Activate use of filetype, indent, and plugin.
filetype on
filetype indent on
filetype plugin on

" Syntax highlighting.
syntax enable

" More info during some commands (like visual selection).
set showcmd

" Fine-tune how VIM formats comments and long lines.
set formatoptions+=croql

" Win32 specific config. {{{

if has('win32')

  if !executable('sh') && !empty($CYGPATH)
    let $PATH = $CYGPATH.'/bin;'.$PATH
  end

  " Look for a real shell.
  if executable('sh')
    set shell=sh
    set shellcmdflag=-c
    set shellpipe=2&>1\|\ tee
    set shellquote=
    set shellredir=>%s\ 2>&1
    set shellslash
    set shelltemp
    set shellxquote=\"
  endif

else

  set shellpipe=\|&\ tee\ %s;\ exit\ $pipestatus\[1\]

  " Bad, bad Windows... Force encoding to UTF-8
  " and fix detection of file encodings.
  set encoding=utf-8 fileencodings=ucs-bom,utf-8,default,latin1

endif

" }}}

" Status line. {{{

" Show statusline even if only one window is visible.
set laststatus=2

" Better status line:
"
" - file name/type/format/encoding
" - diff mode
" - buffer modified/readonly
" - position
"
set statusline=
      \%<%f\ %h%y\ %{&ff}\ %{&fenc}
      \%(\ %{&diff?'['.(&diffopt=~'\\<iwhite\\>'?'d':'D').']':''}%)\ 
      \%(\ %m%)%(\ %r%)%=%-24.
      \(\ %l,%c%V\ [%b,0x%B]\ %)\ %P

" }}}

" Need -w to correctly parse compilation errors when switching directories.
" Multiple jobs will confuse VIM, so disable those too.
set makeprg=make\ -w\ -j1

" Put swap files in $USERVIM/swap or ~/tmp
" (fallback to current dir if not possible).
let swap = $USERVIM.'/swap'
if !isdirectory(swap)
  call mkdir(swap)
endif
let &directory = swap.'//,~/tmp//,.'

" Terminal only settings. {{{

if !has('gui_running')

  " Load specific term keys setting.
  " Needed for some mappings to work properly.
  let keys = $USERVIM.'/keys/'.$TERM
  if filereadable(keys)
    execute 'source ' . keys
    if exists('g:term_keys')
      let s:term_keys =
            \ {
            \ 'C-BS'       : 'cin',
            \ 'C-Del'      : 'cin',
            \ 'C-End'      : 'ci ',
            \ 'C-Home'     : 'ci ',
            \ 'C-Left'     : 'ci ',
            \ 'C-PageDown' : 'ci ',
            \ 'C-PageUp'   : 'ci ',
            \ 'C-Right'    : 'ci ',
            \ 'F1'         : 'n',
            \ 'F2'         : 'n',
            \ 'F3'         : 'n',
            \ 'F4'         : 'n',
            \ 'F5'         : 'n',
            \ 'F6'         : 'n',
            \ 'F7'         : 'n',
            \ 'F8'         : 'n',
            \ 'F9'         : 'n',
            \ 'F10'        : 'n',
            \ 'F11'        : 'n',
            \ 'F12'        : 'n',
            \ 'M-0'        : 'n',
            \ 'M-1'        : 'n',
            \ 'M-2'        : 'n',
            \ 'M-3'        : 'n',
            \ 'M-4'        : 'n',
            \ 'M-5'        : 'n',
            \ 'M-6'        : 'n',
            \ 'M-7'        : 'n',
            \ 'M-8'        : 'n',
            \ 'M-9'        : 'n',
            \ 'M-Down'     : 'n',
            \ 'M-Left'     : 'n',
            \ 'M-Right'    : 'n',
            \ 'M-Up'       : 'n',
            \ 'S-Down'     : 'i ',
            \ 'S-Left'     : 'i ',
            \ 'S-Right'    : 'i ',
            \ 'S-Up'       : 'i ',
            \ }
      for key_name in keys(s:term_keys)
        if !has_key(g:term_keys, key_name)
          continue
        endif
        let key_code = g:term_keys[key_name]
        let key_modes = s:term_keys[key_name]
        for n in range(strlen(key_modes))
          let mode = key_modes[n]
          let map = mode.'map '.key_code.' <'.key_name.'>'
          exe map
        endfor
      endfor
    endif
  endif

  " Fast escape key.
  set timeout timeoutlen=3000 ttimeoutlen=100

  " Colorscheme.
  let g:moria_style = 'black'
  set background=dark
  if 'linux' != $TERM
    colorscheme moria_256
  endif

  " Enable mouse support for all modes but command line.
  set mouse=nvi

  " No beep, and no visual bell.
  set vb t_vb=

endif

" }}}

" Backspace/Delete behaviour. {{{

" Allow backspacing over everything in insert mode.
set backspace=2

" Backward delete word and delete word in insert and command mode.
cmap <C-BS> <C-w>
imap <C-BS> <C-w>
nmap <C-BS> db
cmap <C-Del> <C-Right><C-BS>
imap <C-Del> <C-o>de
nmap <C-Del> de

" Make backspace backward delete a char even in normal mode.
map <BS> X

" }}}

" Cscope support. {{{

if has('cscope')

  if strlen($CSCOPE_DB)
    let g:cscopedb=$CSCOPE_DB
  else
    let g:cscopedb='cscope.out'
  endif

  set cscopequickfix=s-,g-,d-,c-,t-,e-,f-,i-
  set cspc=3
  set csverb

  " Check for either stock cscope executable or for the multi-lingual cscope
  " version (which is the one available in Cygwin).
  if !executable(&cscopeprg)
    if executable('cscope')
      set cscopeprg=cscope
    elseif executable('mlcscope')
      set cscopeprg=mlcscope
    endif
  endif

  " Automatically add database if it exists.
  if filereadable(g:cscopedb)
    exe 'silent cs add '.g:cscopedb
  endif

  " Uses cstag (and so cscope if available...).
  set cst
  " Search the tags database before the cscope database...
  set csto=1

  function CscopeUpdate()

    exe 'silent! cs kill '.fnamemodify(g:cscopedb, ':p')

    let cmd = '!'.&cscopeprg.' -q -b -f '.g:cscopedb
    let files = 'cscope.files'

    if filereadable(files)
      let cmd .= ' -i '.files
    else
      let cmd .= ' -R -s .'
    endif

    exe cmd
    exe 'silent cs add '.g:cscopedb

  endfunction

  " Mappings.
  nmap <unique> <silent> <Leader>tg <C-]>
  nmap <unique> <silent> <Leader>ts :exe 'cscope find s <cword>'<CR>
  nmap <unique> <silent> <Leader>td :exe 'cscope find d <cword>'<CR>
  nmap <unique> <silent> <Leader>tc :exe 'cscope find c <cword>'<CR>
  nmap <unique> <silent> <Leader>ta :exe 'cscope find t <cword>'<CR>
  nmap <unique> <silent> <Leader>tf :exe 'cscope find f <cfile>'<CR>
  nmap <unique> <silent> <Leader>ti :exe 'cscope find i %:t'<CR>
  nmap <unique> <silent> <Leader>tu :call CscopeUpdate()<CR>

endif

" }}}

" Ctags support. {{{

function CtagsUpdate()

  let cmd = '!ctags '.g:ctags_flags.' -o tags -R '
  let files = 'tags.files'

  if filereadable(files)
    let cmd .= '-L '.files
  else
    let cmd .= '.'
  endif

  exe cmd

endfunction

" To update a ctags database.
let g:ctags_flags = '--c-kinds=+cdefgmnpstuvx --fields=+S'
nmap <unique> <Leader>tU :call CtagsUpdate()<CR>

" }}}


" DirDiff options.
let g:DirDiffExcludes = 'CVS,.svn,.bzr,.git,.hg,cscope*.out*,tags,*_debug,*.~[0-9]~,*.~[0-9][0-9]~,*.orig'
let g:DirDiffAddArgs = '--strip-trailing-cr'
let g:DirDiffVerboseSync = 0
let g:DirDiffDynamicDiffText = 1
let g:DirDiffQuitConfirm = 0

" Support for automatically using DirDiff when calling 'vim -d dir1 dir2'. {{{

function VimDirDiff()

  let dir1 = fnameescape(argv(0))
  let dir2 = fnameescape(argv(1))
  silent! exe 'bd '.dir1
  silent! exe 'bd '.dir2
  silent exe 'DirDiff '.dir1.' '.dir2
  windo filetype detect

endfunction

if &diff && 2 == argc() && isdirectory(argv(0)) && isdirectory(argv(1))
  autocmd VimEnter * call VimDirDiff()
endif

" }}}

" }}}

" Plugins. {{{

" Mappings for mark plugin.
nmap <unique> <silent> <leader>mm <Plug>MarkSet
vmap <unique> <silent> <leader>mm <Plug>MarkSet
nmap <unique> <silent> <leader>mr <Plug>MarkRegex
vmap <unique> <silent> <leader>mr <Plug>MarkRegex
nmap <unique> <silent> <leader>mn <Plug>MarkClear

" Configure netrw plugin to use wget rather than elinks; this fix the case
" where VIM will ask and fetch missing dictionaries.
let g:netrw_http_cmd = 'wget -O'
" Also disable banner.
let g:netrw_banner = 0
" Do not show dotfiles per default.
let g:netrw_hide = 1
let g:netrw_list_hide = '\(^\|\s\s\)\zs\.\S\+'

" Configure commentary plugin.
xmap ,c <Plug>Commentary
nmap ,c <Plug>Commentary
nmap ,cc <Plug>CommentaryLine
nmap ,cu <Plug>CommentaryUndo

" Don't load rb_align plugin if ruby feature is not available.
if !has('ruby')
  let loaded_rb_align = 1
end


" }}}


" Quick navigation to the current buffer directory.
nmap <silent> ,d :e %:h<CR>

" }}}

" For iterating on errors.
nmap <silent> <M-k> :cp<CR>
nmap <silent> <M-j> :cn<CR>

" Use space to scroll one page down.
map <space> <C-f>

" }}}

" Trailing spaces/tabs highlighting. {{{

set listchars+=tab:__,trail:_

function! ToggleListBlanks()
  if &list
    setlocal nolist
  else
    setlocal list
  endif
endfunction

map <silent> <F5> :call ToggleListBlanks()<CR>

" }}}

" Search highlighting. {{{

function! ToggleSearchHighlighting()
  if &hlsearch
    set nohlsearch
  else
    set hlsearch
  endif
endfunction

map <silent> <F6> :call ToggleSearchHighlighting()<CR>

" }}}

" Indent and retab. {{{

function! Indent_and_Retab() range
  if &list
    silent! execute a:firstline.','.a:lastline.'s/\s*\r\?$'
  end
  execute a:firstline.','.a:lastline.'retab'
  execute 'normal '.a:firstline.'G='.a:lastline . 'G'
endfunction

function! Indent_and_Retab_operator(type)
  :'[,']call Indent_and_Retab()
endfunction

vmap <silent> ,= :call Indent_and_Retab()<CR>
nmap <silent> ,= :set opfunc=Indent_and_Retab_operator<CR>g@
nmap <silent> ,== :.call Indent_and_Retab()<CR>

" }}}

" Swap two pieces of text. {{{

function! SwapText(start, end, visual)

  let b1 = line2byte(line("'.")) + col("'.") - 1
  let b2 = line2byte(line("'".a:start)) + col("'".a:start)

  if b1 > b2
    let s1 = strlen(getreg())
    let s2 = line2byte(line("'".a:end)) + col("'".a:end) - b2 + 1
    let b1 -= s2 - s1
  end

  if a:visual
    exe 'normal gvP'
  else
    exe 'normal `'.a:start.'v`'.a:end.'P'
  endif
  exe 'goto '.b1
  exe 'normal "*P'

endfunction

function! SwapText_operator(type)
  call SwapText('[', ']', 0)
endfunction

function! SwapText_visual() range
  call SwapText('<', '>', 1)
endfunction

function! SwapText_line()
  exe 'normal V,s'
endfunction

vmap <silent> ,s :call SwapText_visual()<CR>
nmap <silent> ,s :set opfunc=SwapText_operator<CR>g@
nmap <silent> ,ss :call SwapText_line()<CR>

" }}}

" Cursor cross.
if has('autocmd')
  autocmd BufWinEnter * set cursorline cursorcolumn
  autocmd WinEnter    * set cursorline cursorcolumn
  autocmd WinLeave    * set nocursorline nocursorcolumn
endif


" Python. {{{

if has('python')

python <<EOF

from sys import path
import vim

path.insert(0, vim.eval('$USERVIM') + '/lib/python')

EOF

endif

" }}}

" Ruby. {{{

if has('ruby')

ruby <<EOF

$:.insert(0, VIM::evaluate('$USERVIM') + '/ruby')

EOF

endif

" }}}

" vim: ft=vim sw=2 foldmethod=marker foldlevel=0
